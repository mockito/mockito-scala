apply plugin: "org.shipkit.shipkit-auto-version"
apply plugin: "org.shipkit.shipkit-changelog"
apply plugin: "org.shipkit.shipkit-github-release"

tasks.named("generateChangelog") {
    previousRevision = project.ext.'shipkit-auto-version.previous-tag'
    githubToken = System.getenv("GITHUB_TOKEN")
    repository = "mockito/mockito-scala"
}

tasks.named("githubRelease") {
    def genTask = tasks.named("generateChangelog").get()
    dependsOn genTask
    repository = genTask.repository
    changelog = genTask.outputFile
    githubToken = System.getenv("GITHUB_TOKEN")
    newTagRevision = System.getenv("GITHUB_SHA")
}

apply plugin: 'com.jfrog.bintray'

//Bintray configuration is handled by JFrog Bintray Gradle Plugin
//For reference see the official documentation: https://github.com/bintray/gradle-bintray-plugin
bintray {

    key = System.getenv("BINTRAY_API_KEY")

    pkg {
        repo = 'maven'
        user = 'szczepiq'
        name = 'mockito-scala'
        userOrg = 'mockito'
        licenses = ['MIT']
        labels = ['mocks', 'tdd', 'unit tests']
        publish = true // can be changed to 'false' for testing
        dryRun = project.hasProperty("bintrayDryRun")

        filesSpec {
            // all contents of this directory will be uploaded to Bintray
            from fileTree("target/dist")
            into '.'
        }

        version {
            mavenCentralSync {
                sync = true
                user = System.env.NEXUS_TOKEN_USER
                password = System.env.NEXUS_TOKEN_PWD
            }
        }
    }
}
